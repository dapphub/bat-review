<!doctype html>
<meta charset=utf-8>
<meta name=viewport content=width=device-width>
<link rel=stylesheet href=index.css>
<script id=content type=markdown>

# Brave Access Token/Crowdsale Code Review

#### Nikolai Mushegian, DappHub

DISCLAIMER/SCOPE
---

This document is our best attempt to review the Solidity source code at commit [04866d03](https://github.com/brave/BAT-prototyping/tree/04866d03cb42790287d8c38f3d4a334277527924/contracts) of the private BAT prototype crowdsale repository. This is not a verification of any objects that have been deployed to the Ethereum blockchain.

It is important to note that no system is safe until proper verification tools exist. Solidity compiler errors are still common, and it is also not yet feasible to verify that the compiler or emitted code are correct.

### List of Files

    Migrations.sol
    SafeMath.sol
    StandardToken.sol
    testBAToken.sol

CRITICAL BLOCKING ISSUES
------------------------

We found no critical blocking issues in the repository.

MAJOR ISSUES
---

### Harmful special cases in StandardToken that could result in stuck funds

`StandardToken` includes deceptively harmful special cases which can lead to funds getting stuck if consumer contracts don't take
care to work around them.

Treating 0-value transfer and approvals as valid makes for far cleaner token semantics - returning false on 0 is an anti-pattern.

This is an example code snippet of a contract using BAT for some incentive, which might seem correct at first glance,
but could actually freeze funds inside contract:

```
uint _reward = 1000;
// suppose the authority can lower the reward
function lowerReward(uint what) auth {
    assert( what < _reward );
    _reward = what;
}
function claimReward() {
    assert( BAT.transfer(recipient, amount) && BAT.transfer(msg.sender, _reward) );
}
```

There is not consensus among token developers about whether this is a real problem, but the DappHub team maintains our recommendation
to change the default behavior of StandardToken. The issue is being discussed in the [Tokens project repo](https://github.com/ConsenSys/Tokens/issues/47).

### Several minor changes since review commit

There were several commits added to the source repository after a review tag was identified. The changes look fine,
but are technically out of scope. We've noted in this document where our suggested changes appeart to have been implemented in the finalized BAT [crowdsale repository](https://github.com/brave-intl/basic-attention-token-crowdsale/).

MINOR ISSUES AND SUGGESTIONS
---

### Assertions and preconditions could be significantly clearer.

E.G.:
```
      if ((block.number <= fundingEndBlock ||
           totalSupply < tokenCreationMin) &&
          totalSupply < tokenCreationCap) throw;
```

vs

```
var done = block.number <= fundingEndBlock;
var enough = totalSupply >= tokenCreationMin;
assert( done && enough ); 
```

(One condition eliminated completly as it is necessarily true due to `createToken` logic)

**Update 05/23/17:** The assertions in the finalized repo appear to have been split up and the legibility improved.

### `finalize` state transition could be defined implicitly, avoiding need for admin transaction

All information necessary to virtualize the `finalize` state transition is available at all times,
as it only uses timestamps and local storage variables.

### Treating 0-value messages as special cases is usually an anti-pattern.

This is related to the blocking issue above, though is far less likely to case problems:

```
if (msg.value == 0) throw;
```

If this line is deleted, the entire action behaves the same way.

### Control address must not revert on call or else all funds could be lost.

```
if(!attnTokenControl.send(this.balance)) throw;  // send the eth home
```

The developers must explicitly take care to not to deploy this from any utility or control contracts (e.g. a multisig)
with a throwing fallback function or one that uses more than default `.send` gas, which is only enough for one event log.

The developers do indeed plan to use a standard multisig contract which will not have any problems. The danger here is
only related to transferring control to a contract that uses more than 21000 gas in the fallback. All examples feel contrived
and unlikely to happen in the wild, but are worth noting with so much money at stake.

```
contract BadWallet {
    // ... multisig features
    function() {
        // While this seems pointless, it also doesn't seem obviously dangerous.
        _calls++;
        _callers[_calls] = msg.sender;
    }
}
```


We recommneded to simply switch to `.call.value()`-based transfers (see rewrite section and `ds-exec`).

### User addresses must not revert on call or else their funds could be lost.

```
if (!msg.sender.send(ethVal)) throw;
```

The same issue exists for user addresses used to call `createToken/refund`. This is slightly more concerning than the previous issue
because the developers have no control over how users choose to interact with their contract.

This is a theoretical edge case that is not likely to cause problems, and this falls into the long tail of ways it's possible for a user to trap their own tokens.

One suggestion is that the developers explicitly warn users to only use the most common multisig contracts.

Better yet, we suggest switching to `.call.value()`-based transfers (see rewrite section and `ds-exec`).

### Funding start/end should probably use timestamps, not blocks.

Expected blocktime-time error increases proportionally to length of crowdsale.

### Intent could be clarified with better names

Two examples are `attnTokenControl` or `tmp`.

**Update 05/23/17:** The variable names in the finalized repo appear to have been renamed and the legibility improved.

### Manual token accounting and overloading Transfer event

There is duplicated accounting logic for creating/destroying tokens and signaling this with an event; we suggest studying `mint`/`burn`
functions in our sample rewrite and adopting similar patterns.

### Inconsistent use of modifiers vs explicit assertions

Readability would be improved if there were more consistency in choice of modifier-based preconditions vs explicit assertions.

E.G:

```
    function refund()
      public
      onlyWhenFunding
    {
```

vs

```
    function refund()
      public
    {
      if(!isFunding) throw;
```

**Update 05/23/17:** The use of all modifiers in the finalized repo appears to have been removed.

### Token ratio is arbitrary

It's strange not to use a 1:1 ratio of BAT to ETH just to get 5 more figures of effective precision when 18 decimals seems to be more than enough.
If an initial price of 1:1 is used, the impact of the inflation for the developers is easier to understand as well.

### Custodian/developer fund allocation strategy could use justification

The allocation strategy for the custodian/development fund does not appear to be engineered to achieve any specific incentives.
It would be good to justify the particular constants chosen for the limits, as well as for the distribution strategy as a whole.

SAMPLE REWRITE
---

```
// (C) 2017 DappHub, LLC

pragma solidity ^0.4.8;

import 'ds-token/token.sol';
import 'ds-math/math.sol';
import 'ds-exec/exec.sol';

contract BATCrowdsale is DSAuth // access control
                       , DSMath // safe math
                       , DSExec // safe calls
{
    DSToken BAT;

    address custodian;
    uint custodianRatio; // as a whole number of percent, e.g. 30 -> 30%
    uint limit;
    uint minimum;
    uint saleStart;
    uint saleEnd;
    bool done; // set true on finalize
    bool ok;   // set true on finalize only if all conditions met

    function BATCrowdsale(address custodian_, uint saleStart_, uint saleEnd_)
    {
        BAT = new DSToken("BAT", "Brave Access Token", 18);
        BAT.stop(); // Don't allow ERC20 actions until/unless sale succeeds
        custodian = custodian_;
        saleStart = saleStart_;
        saleEnd = saleEnd_;
    }
    function buy()
        payable
    {
        assert( saleStart <= now && now <= saleEnd );
        assert( BAT.totalSupply() + msg.value <= limit );
        // TODO if you want something other than 1:1, do conversion here
        var payout = cast(msg.value);
        BAT.mint(payout);
        BAT.push(msg.sender, payout);
    }
    function finalize()
        auth
    {
        assert( saleEnd < now );
        var supply = BAT.totalSupply();
        done = true;
        ok = (minimum <= supply) && (supply <= limit);
        if( ok ) { // create dev allocation and pop off token
            // TODO update with formula specified in original
            var reward = cast(supply * custodianRatio / 100);
            BAT.mint(reward);
            BAT.push(custodian, reward);
            BAT.start(); // re-enable ERC20 actions
            BAT.setOwner(msg.sender);
            exec(msg.sender, this.balance);
        }
    }
    function refund()
    {
        assert( done && !ok );
        var bal = cast(BAT.balanceOf(msg.sender));
        BAT.pull(msg.sender, bal);
        BAT.burn(bal);
        exec(msg.sender, uint(bal));
    }
}
```

Source and dependencies available at [github.com/dapphub/bat-rewrite](https://github.com/dapphub/bat-rewrite).

Note that this code has no tests, is not a drop-in equivalent to the BAT sale/token, and is not suitable for production.

</script>
<script src=showdown.min.js></script>
<script>document.write(new showdown.Converter().makeHtml(document.getElementById("content").innerHTML))</script>

